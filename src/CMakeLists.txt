include(ExternalProject)

ExternalProject_Add(UFS_UTILS
  PREFIX ${CMAKE_CURRENT_BINARY_DIR}/UFS_UTILS
  SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/UFS_UTILS
  INSTALL_DIR ${CMAKE_INSTALL_PREFIX}
  CMAKE_ARGS "-DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}"
  BUILD_ALWAYS TRUE
  )

if(NOT CCPP_SUITES)
  set(CCPP_SUITES "FV3_GFS_2017_gfdlmp,FV3_GFS_2017_gfdlmp_regional,FV3_GFS_v15p2,FV3_GFS_v16,FV3_RRFS_v1beta,FV3_HRRR,FV3_RRFS_v1alpha,FV3_GFS_v15_thompson_mynn_lam3km")
endif()

if(NOT APP)
  set(APP "ATM")
endif()

if(NOT CMAKE_BUILD_TYPE)
  set(CMAKE_BUILD_TYPE "RELEASE")
endif()

list(APPEND UFS_WEATHER_MODEL_ARGS
  "-DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}"
  "-DCCPP_SUITES=${CCPP_SUITES}"
  "-DCMAKE_C_COMPILER=${MPI_C_COMPILER}"
  "-DCMAKE_CXX_COMPILER=${MPI_CXX_COMPILER}"
  "-DCMAKE_Fortran_COMPILER=${MPI_Fortran_COMPILER}"
  "-DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}"
  "-DCMAKE_MODULE_PATH=${MAPL_ROOT}/share/MAPL/cmake"
  "-DNETCDF_DIR=$ENV{NETCDF}"
  "-D32BIT=ON"
  "-DINLINE_POST=ON"
  "-DAPP=${APP}"
)

string(TOUPPER "${CMAKE_BUILD_TYPE}" TOUPPER_CMAKE_BUILD_TYPE)
if (TOUPPER_CMAKE_BUILD_TYPE MATCHES "DEBUG")
  list(APPEND UFS_WEATHER_MODEL_ARGS "-DDEBUG=ON")
endif()

if (ENABLE_OPTIONS)
  string(REPLACE "," ";" ENABLE_OPTIONS "${ENABLE_OPTIONS}")
  foreach (option_on IN ITEMS ${ENABLE_OPTIONS})
    list(APPEND UFS_WEATHER_MODEL_ARGS "-D${option_on}=ON")
  endforeach()
endif()

if (DISABLE_OPTIONS)
  string(REPLACE "," ";" DISABLE_OPTIONS "${DISABLE_OPTIONS}")
  foreach (option_off IN ITEMS ${DISABLE_OPTIONS})
    list(APPEND UFS_WEATHER_MODEL_ARGS "-D${option_off}=OFF")
  endforeach()
endif()

ExternalProject_Add(ufs-weather-model
  PREFIX ${CMAKE_CURRENT_BINARY_DIR}/ufs-weather-model
  SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/ufs-weather-model
  INSTALL_DIR ${CMAKE_INSTALL_PREFIX}
  CMAKE_ARGS ${UFS_WEATHER_MODEL_ARGS}
  INSTALL_COMMAND mkdir -p ${CMAKE_INSTALL_PREFIX}/bin && cp ${CMAKE_CURRENT_BINARY_DIR}/ufs-weather-model/src/ufs-weather-model-build/ufs_model ${CMAKE_INSTALL_PREFIX}/bin/
  BUILD_ALWAYS TRUE
  )

ExternalProject_Add(UPP
  PREFIX ${CMAKE_CURRENT_BINARY_DIR}/UPP
  SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/UPP
  INSTALL_DIR ${CMAKE_INSTALL_PREFIX}
  CMAKE_ARGS "-DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}"
  BUILD_ALWAYS TRUE
  )
